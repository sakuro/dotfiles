#!/bin/zsh

function prompt_sakuro_setup()
{
  add-zsh-hook precmd prompt_sakuro_precmd
}

function prompt_sakuro_precmd()
{
  autoload -Uz vcs_info
  zstyle ':vcs_info:*'     formats       '%s%F{cyan}:%f%F{green}%b%f' '%S'
  zstyle ':vcs_info:*'     actionformats '%s%F{cyan}:%f%b%F{cyan}:%F{red}%a%f'
  zstyle ':vcs_info:svn:*' branchformat  '%F{green}%b%F{cyan}:%fr%r'
  zstyle ':vcs_info:*' enable git svn hg bzr cvs

  local vcs_info_msg_0_ vcs_info_msg_1_
  LANG=C vcs_info
  # [pwd]
  if [[ -z "$vcs_info_msg_0_" || $vcs_info_msg_1_ = '.' ]]; then
    psvar=( "%~" )
  else
    # subdirectory part is show in different color
    psvar=( "${$(print -P '%~')%/$vcs_info_msg_1_}%f/%F{cyan}$vcs_info_msg_1_" )
  fi
  psvar[1]="%F{gray}[%F{yellow}$psvar[1]%F{gray}]%f"
  # +[vcs]
  [[ -n "$vcs_info_msg_0_" ]] && psvar[1]+=" %F{gray}[%f$vcs_info_msg_0_%F{gray}]%f"
  # +[rbenv]
  psvar[1]+=" %F{gray}[%F{green}$(rbenv version-name)%F{gray}]%f"

  # $
  psvar+='%(?,%F{cyan},%F{red})%(#,#,$)%f '
  PROMPT=${(F)psvar}
}

prompt_sakuro_setup "$@"
