#!/bin/zsh

function prompt_sakuro_setup()
{
  autoload -Uz vcs_info
  zstyle ':vcs_info:*'     formats       '%s%F{cyan}:%f%F{green}%b%f' '%S'
  zstyle ':vcs_info:*'     actionformats '%s%F{cyan}:%f%b%F{cyan}:%F{red}%a%f'
  zstyle ':vcs_info:svn:*' branchformat  '%F{green}%b%F{cyan}:%fr%r'
  zstyle ':vcs_info:*' enable git svn hg bzr cvs

  function precmd()
  {
    LANG=C vcs_info
    # [pwd]
    if [[ -z "$vcs_info_msg_0_" || $vcs_info_msg_1_ = '.' ]]; then
      psvar=( "%~" )
    else
      # subdirectory part is show in different color
      psvar=( "${$(print -P '%~')%/$vcs_info_msg_1_}%f/%F{cyan}$vcs_info_msg_1_" )
    fi
    psvar[1]="%F{gray}[%F{yellow}$psvar[1]%F{gray}]%f"
    # +[vcs]
    [[ -n "$vcs_info_msg_0_" ]] && psvar[1]+=" %F{gray}[%f$vcs_info_msg_0_%F{gray}]%f"
    # +[rvm]
    is_executable rvm-prompt && psvar[1]+=" %F{gray}[%frvm%F{cyan}:%F{green}$(rvm-prompt i v p g s)%F{gray}]%f"

    # %
    psvar+='%(?,%F{cyan},%F{red})%#%f '
    # host[window]+
    in_screen && psvar[2]="${WINDOW:+[$WINDOW]}$psvar[2]"
    psvar[2]="$(load_avg)%m%f$psvar[2]"

    PROMPT=${(F)psvar}
  }
}
