#!/bin/zsh

function prompt_sakuro_setup()
{
  autoload -Uz vcs_info
  zstyle ':vcs_info:*'     formats       '%s%F{cyan}:%f%F{green}%b%f' '%S'
  zstyle ':vcs_info:*'     actionformats '%s%F{cyan}:%f%b%F{cyan}:%F{red}%a%f'
  zstyle ':vcs_info:svn:*' branchformat  '%F{green}%b%F{cyan}:%fr%r'
  zstyle ':vcs_info:*' enable git svn hg bzr cvs

  function precmd()
  {
    local LB="%F{gray}[%f"
    local RB="%F{gray}]%f"

    LANG=C vcs_info
    # [pwd]
    if [[ -z "$vcs_info_msg_0_" || $vcs_info_msg_1_ = '.' ]]; then
      psvar=( "%~" )
    else
      # subdirectory part is show in different color
      psvar=( "${$(print -P '%~')%/$vcs_info_msg_1_}%f/%F{cyan}$vcs_info_msg_1_" )
    fi
    psvar[1]="$LB%F{yellow}$psvar[1]$RB"
    # +[vcs]
    [[ -n "$vcs_info_msg_0_" ]] && psvar[1]+=" $LB$vcs_info_msg_0_$RB"
    # +[rvm]
    is_executable rvm-prompt && psvar[1]+=" ${LB}rvm%F{cyan}:%F{green}$(rvm-prompt i v p g s)$RB"

    local marker='%(?,%F{cyan},%F{red})%#%f'
    if in_screen; then
      # $ 
      psvar+="$marker "
    else
      # host[window]$ 
      psvar+="$(load_avg)%m%f${WINDOW:+[$WINDOW]}$marker "
    fi
    PROMPT=${(F)psvar}
  }
}
