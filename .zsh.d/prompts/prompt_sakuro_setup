#!/bin/zsh

function prompt_sakuro_setup()
{
  zstyle ':vcs_info:*'     formats       '%s%B%F{cyan}:%f%B%F{blue}%b%f' '%S'
  zstyle ':vcs_info:*'     actionformats '%s%B%F{cyan}:%f%b%B%F{cyan}:%B%F{red}%a%f'
  zstyle ':vcs_info:svn:*' branchformat  '%B%F{blue}%b%B%F{cyan}:%fr%r'
  zstyle ':vcs_info:*' enable git svn hg bzr cvs

  add-zsh-hook precmd prompt_sakuro_precmd
}

function prompt_sakuro_precmd()
{
  autoload -Uz vcs_info

  local vcs_info_msg_0_ vcs_info_msg_1_
  LANG=C vcs_info

  # [pwd]
  local base_path="$(print -P '%~')"
  # remove repository_part($vcs_info_msg_1_)
  [[ -n "$vcs_info_msg_0_" && $vcs_info_msg_1_ != '.' ]] && base_path="${base_path%/$vcs_info_msg_1_}"

  typeset -la path_elements
  set -A path_elements ${(s:/:)base_path}

  if [[ $#path_elements -gt 2 ]]; then
    local first="$path_elements[1]"
    [[ "$first" = '~'* ]] || first="/$first"
    local last="$path_elements[-1]"
    base_path="$first/"${(j:/:)$(print -P '%2>.>'${^${(@)path_elements[2,-2]}})}/"$last"
  fi

  psvar=( "%F{gray}[%B%F{magenta}$base_path" )
  [[ -n "$vcs_info_msg_1_" && "$vcs_info_msg_1_" != '.' ]] && psvar[1]+="%f%b/%B%F{cyan}$vcs_info_msg_1_"
  psvar[1]+="%f%b%F{gray}]%f%b"

  # +[vcs]
  [[ -n "$vcs_info_msg_0_" ]] && psvar[1]+=" %F{gray}[%f%b$vcs_info_msg_0_%F{gray}]%f%b"
  # +[rbenv]
  psvar[1]+=" %F{gray}[%B%F{blue}$(rbenv version-name)%f%b%F{gray}]%f%b"

  # host+
  [[ -z "$TMUX_PANE" ]] && psvar[2]="$(load_avg)%m%f%b"

  # $
  psvar[2]+='%(?,ðŸ˜„,ðŸ˜¡)  '

  PROMPT=${(F)psvar}
}

prompt_sakuro_setup "$@"
