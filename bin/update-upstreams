#!/bin/zsh

function update-upstream()
{
  [[ -d $repo/.git ]] || {
    echo "${repo/$HOME/~} is not a git directory, skipping"
    return
  }
  (
    cd $repo
    git remote show | grep upstream >/dev/null || return
    echo "=== ${repo/$HOME/~} ==="
    git fetch upstream
    if git diff-index --quiet HEAD; then
      default_branch=$(gh repo view --json defaultBranchRef --jq .defaultBranchRef.name)
      git rebase upstream/$default_branch
      if git remote show | grep origin >/dev/null; then
        git push origin
      fi
    else
      echo "There are uncommitted change(s), skipping rebase and push"
    fi
  )
}

if [[ $# = 0 ]]; then
  while read repo; do
    update-upstream $repo
  done
else
  for repo in "$@"; do
    update-upstream $repo
  done
fi
