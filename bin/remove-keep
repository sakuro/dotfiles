#!/bin/zsh

KEEP_FILE="${KEEP_FILE:-.keep}"

[[ -d .git ]] || {
  echo "Run in the root directory of a git repository"
  exit 1
}

typeset -A opts=()
zparseopts -D -E -A opts c n v

keeps=( "${(ps:\0:)$(git ls-files -z ':**/'"${KEEP_FILE}")}" )
keeps=( "${keeps[@]:#}" )
removed_keeps=0

commit=$(( ${+opts[-c]} ))
dry_run=$(( ${+opts[-n]} ))
verbose=$(( ${+opts[-v]} || dry_run ))

(( dry_run )) && {
  exec > >(sed 's/^/[DRY RUN] /')
}

(( verbose )) && {
  echo "Removing unnecessary $KEEP_FILE files ..."
}

[[ ${#keeps[@]} = 0 ]] && {
  (( verbose )) && {
    echo "No $KEEP_FILE files were found."
  }
  exit 0
}

for keep in "${keeps[@]}"; do
  other_files=( "${(ps:\0:)$(git ls-files -z "${keep:h}" ':!'"${keep}" ':!'"${keep:h}/**/${KEEP_FILE}")}" )
  other_files=( "${other_files[@]:#}" )

  [[ ${#other_files[@]} -gt 0 ]] || continue

  if (( dry_run)); then
    echo git rm "$keep"
  elif (( verbose )); then
    git rm "$keep"
  else
    git rm --quiet "$keep"
  fi

  (( removed_keeps++ ))
done

if [[ "${removed_keeps}" -eq 0 ]]; then
  (( verbose )) && {
    echo "No unnecessary $KEEP_FILE files were found."
  }
  exit 0
else
  (( verbose )) && {
    echo "Removed unnecessary $KEEP_FILE files."
    git status --short
  }
  if (( commit )); then
    if (( dry_run )); then
      echo "git commit -m \"Remove unnecessary $KEEP_FILE files\""
      exit 0
    else
      git commit -m "Remove unnecessary $KEEP_FILE files"
    fi
  else
    if (( verbose )); then
      echo "Run 'git commit -m \"Remove unnecessary $KEEP_FILE files\"' to commit the changes."
    fi
    exit 1
  fi
fi
