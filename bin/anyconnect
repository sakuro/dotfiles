#!/bin/zsh

for rc in "${XDG_CONFIG_HOME:+$XDG_CONFIG_HOME/anyconnectrc}" "${HOME:+$HOME/.anyconnectrc}"; do
  if [[ -f "$rc" ]]; then
    source $rc
    break
  fi
done

host="${ANYCONNECT_HOST?not set}"
service="${ANYCONNECT_SERVICE?not set}"
interface="${ANYCONNECT_INTERFACE?not set}"

vpn="/opt/cisco/anyconnect/bin/vpn"

[[ -x "$vpn" ]] || {
  echo "Command not found: $vpn:t"
  exit 1
}


case $1 in
connect|on)
  {
    security find-generic-password -s "$service" | grep -Po '(?<=acct"<blob>=").*(?=")'
    security find-generic-password -s "$service" -w
  } | $vpn connect "$host" -s
  ;;
disconnect|off)
  $vpn disconnect "$host"
  ;;
status)
  (( ${"$(ifconfig -l)"[(I)$ANYCONNECT_INTERFACE]} )) && {
    echo "on"
  } || {
    echo "off"
  }
  ;;
*)
  echo "Usage:"
  echo "$0:t connect | on"
  echo "$0:t disconnect | off"
  echo "$0:t status"
  exit 1
  ;;
esac
