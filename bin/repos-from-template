#!/bin/zsh
#
# repos-from-template - List repositories created from a specific template
#
# Queries GitHub GraphQL API to find all repositories owned by you
# that were created from the specified template repository.
# Output is JSON array with nameWithOwner, url, and template info.
#
# Usage:
#   repos-from-template owner/template-repo
#
# Examples:
#   repos-from-template sakuro/gem-scaffold
#   repos-from-template my-org/project-template | jq -r '.[].nameWithOwner'
#
# Combined with update-secret to set secrets across all derived repositories:
#   echo "SECRET_VALUE" | update-secret SECRET_NAME \
#     $(repos-from-template owner/template | jq -r '.[].nameWithOwner')
#
# Requires: gh (GitHub CLI), jq

set -euo pipefail

if [[ $# != 1 ]]; then
  echo "Usage: $0:t owner/repo"
  exit 1
fi

TEMPLATE_REPO="$1"
TEMPLATE_OWNER="${TEMPLATE_REPO%%/*}"
TEMPLATE_NAME="${TEMPLATE_REPO#*/}"

TEMPLATE_ID="$(
  gh api graphql \
    -F owner="$TEMPLATE_OWNER" -F name="$TEMPLATE_NAME" \
    -f query='
      query($owner: String!, $name: String!) {
        repository(owner: $owner, name: $name) { id nameWithOwner }
      }
    ' \
  | jq -r '.data.repository.id'
)"

gh api graphql --paginate --slurp \
  -f query='
    query($endCursor: String) {
      viewer {
        repositories(first: 100, after: $endCursor, ownerAffiliations: OWNER) {
          pageInfo { hasNextPage endCursor }
          nodes {
            nameWithOwner
            url
            templateRepository { id nameWithOwner url }
          }
        }
      }
    }
  ' \
| jq --arg tid "$TEMPLATE_ID" '
  [
    .[]
    | .data.viewer.repositories.nodes[]
    | select(.templateRepository != null and .templateRepository.id == $tid)
    | {
        nameWithOwner,
        url,
        template: .templateRepository
      }
  ]
'

