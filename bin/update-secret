#!/usr/bin/env zsh
#
# update-secret - Set a secret across multiple GitHub repositories
#
# Secret value is read from stdin to avoid exposing it in command line arguments.
#
# Usage:
#   echo "SECRET_VALUE" | update-secret SECRET_NAME owner/repo1 owner/repo2 ...
#
# Combined with repos-from-template to update all repositories derived from a template:
#   echo "SECRET_VALUE" | update-secret SECRET_NAME \
#     $(repos-from-template owner/template | jq -r '.[].nameWithOwner')
#
# Exit codes:
#   0 - All repositories updated successfully
#   1 - Missing arguments
#   2 - One or more repositories failed

set -euo pipefail

if [[ $# -lt 2 ]]; then
  echo "Usage: $0:t SECRET_NAME owner/repo1 owner/repo2 ..."
  exit 1
fi

SECRET_NAME="$1"
shift
REPOS=("$@")

read -r -s SECRET_VALUE

failures=0

for repo in "${REPOS[@]}"; do
  echo "Setting secret ${SECRET_NAME} in ${repo} ..."
  if printf "%s" "${SECRET_VALUE}" | gh secret set "${SECRET_NAME}" -R "${repo}"; then
    echo "OK: ${repo}"
  else
    echo "FAIL: ${repo}" >&2
    failures=$((failures + 1))
  fi
done

if [[ "${failures}" -ne 0 ]]; then
  echo "Done with failures: ${failures}" >&2
  exit 2
fi

echo "Done. Updated ${#REPOS[@]} repositories."
