#!/usr/bin/env zsh
# $1 -- Name of the remote to which the push is being done
# $2 -- URL to which the push is being done

# If pushing without using a named remote those arguments will be equal.
#
# Information about the commits which are being pushed is supplied as lines to
# the standard input in the form:
#
#   <local ref> <local sha1> <remote ref> <remote sha1>

set -ue
set -o errexit -o nounset -o pipefail

remote="$1"
url="$2"

GIT_HOOKS_DIR=$XDG_CONFIG_HOME/git/hooks
. $GIT_HOOKS_DIR/support.zsh

# Remove unnecessary .keep files
if (( $+commands[remove-keep] )); then
  remove-keep -v
fi

# Check if there are duplicated file names with different case
git ls-files | tr '[:upper:]' '[:lower:]' | sort | uniq -d | while read file; do
  echo "Error: Found duplicated file names with different case: $file"
  exit 1
done


# Check LF at EOF
typeset -a missing_eol=()
while :; do
  IFS= read -r -d '' file || break
  IFS= read -r -d '' attr || break
  IFS= read -r -d '' value || break
  [[ $attr == text ]] || continue
  [[ $value == set || $value == unspecified ]] || continue
  [[ -s $file ]] || continue
  tail -c 1 -- "$file" | grep -q $'\n' || missing_eol+=("$file")
done < <(git ls-files -z | git check-attr -z --stdin text)

if (( ${#missing_eol} )); then
  print -- "No newline at end of file:"
  for file in "${missing_eol[@]}"; do
    print -r -- "- $file"
  done
  exit 1
fi

# Run language specific hooks
set -- $(detect-languages)

while [[ $# -gt 0 ]]; do
  hook=$GIT_HOOKS_DIR/pre-push-$1
  shift
  [[ -x $hook ]] || continue
  echo "Running ${hook:t} hook ..."
  $hook $remote $url || exit 1
done

exit 0
