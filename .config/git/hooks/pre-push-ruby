#!/bin/zsh

source "${0:h}/support.zsh"

function need-bundle-update() {
  [[ -f Gemfile ]] && git ls-files --error-unmatch Gemfile.lock >/dev/null 2>/dev/null && [[ Gemfile.lock -ot Gemfile ]]
}

function rubocop-ready() {
  [[ -f .rubocop.yml ]]
}

function run-rubocop() {
  command bundle exec rubocop
}

function rspec-ready() {
  [[ -f .rspec ]]
}

function run-rspec() {
  command bundle exec rspec --format progress
}

if need-bundle-update; then
  echo "Gemfile.lock is not up to date.  Run bundle install"
  return 1
fi

for command in rubocop rspec; do
  command-in-path $command || continue
  $command-ready || continue
  echo "Running $command ..."
  run-$command || return 1
done
