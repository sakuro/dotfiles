#!/bin/zsh

function prompt_sakuro_setup()
{
  zstyle ':vcs_info:*'     formats       '%F{blue}%b%f' '%S'
  zstyle ':vcs_info:*'     actionformats '%b%F{cyan}:%F{red}%a%f'
  zstyle ':vcs_info:svn:*' branchformat  '%F{blue}%b%F{cyan}:%fr%r'
  zstyle ':vcs_info:*' enable git svn hg bzr cvs

  add-zsh-hook precmd prompt_sakuro_precmd
}

function prompt_sakuro_precmd()
{
  autoload -Uz vcs_info

  local vcs_info_msg_0_ vcs_info_msg_1_
  LANG=C vcs_info

  psvar=()

  # user@host
  psvar[1]+="%n%F{cyan}@$(load-average-color)%m"

  # %tmux pane
  [[ -n "$TMUX_PANE" ]] && psvar[1]+="%F{gray}:%F{blue}%$TMUX_PANE"

  # %jobs
  psvar[1]+="%(1j,%F{magenta}%%%j,)"

  # [time]
  psvar[1]+=" %F{yellow}%D{%H:%M:%S}%f"

  # [vcs]
  [[ -n "$vcs_info_msg_0_" ]] && psvar[1]+=" $vcs_info_msg_0_ "

  # [pwd]
  local base_path="$(print -P '%~')"
  # remove repository_part($vcs_info_msg_1_)
  [[ -n "$vcs_info_msg_0_" && $vcs_info_msg_1_ != '.' ]] && base_path="${base_path%/$vcs_info_msg_1_}"

  typeset -la path_elements
  set -A path_elements ${(s:/:)base_path}

  if [[ $#path_elements -gt 2 ]]; then
    local first="$path_elements[1]"
    [[ "$first" = '~'* ]] || first="/$first"
    local last="$path_elements[-1]"
    base_path="$first/"${(j:/:)$(print -P '%2>.>'${^${(@)path_elements[2,-2]}})}/"$last"
  fi

  psvar[1]+="%F{magenta}$base_path%f"
  [[ -n "$vcs_info_msg_1_" && "$vcs_info_msg_1_" != '.' ]] && psvar[1]+="/%F{cyan}$vcs_info_msg_1_%f"

  # $
  psvar[2]+='%(!.#.$) '

  PROMPT=${(F)psvar}
}

prompt_sakuro_setup "$@"
