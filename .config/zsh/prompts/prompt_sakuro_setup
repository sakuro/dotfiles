#!/bin/zsh

function prompt_sakuro_setup() {
  zstyle ':vcs_info:*'     formats       '%b' '%S'
  zstyle ':vcs_info:*'     actionformats '%b %a'
  zstyle ':vcs_info:*' enable git

  source ~/.config/zsh/prompts/prompt_duration
  add-zsh-hook precmd prompt_sakuro_precmd
}

function prompt_sakuro_precmd() {
  local last_status=$?
  autoload -Uz vcs_info

  psvar=()

  [[ -n "$prompt_duration" ]] && print -P "$prompt_duration"

  local -a info_line_elements=(
#    "$(prompt_sakuro_user)@$(prompt_sakuro_host)"
    "$(prompt_sakuro_pwd)"
    "$(prompt_sakuro_vcs)"
    "$(prompt_sakuro_ruby)"
    "$(prompt_sakuro_kube)"
    "$(prompt_sakuro_aws)"
  )
  local -a prompt_line=(
    "$(prompt_sakuro_prompt $last_status)"
  )

  psvar+="${(j: :)info_line_elements}"
  psvar+="${prompt_line} "

  PROMPT=${(F)psvar}
}

function prompt_sakuro_user() {
  print -n "${(%):-'%n'}"
}


function prompt_sakuro_host() {
  local -i ncpu

  if [[ -f /proc/cpuinfo ]]; then
    set -- ${(M)"${(@f)$(</proc/cpuinfo)}"#processor*:*}
    ncpu=$#
  else
    ncpu=$(sysctl -n hw.ncpu)
  fi

  if [[ -r /proc/loadavg ]]; then
    set -- $(< /proc/loadavg)
  elif [[ -x /usr/sbin/sysctl ]]; then
    set -- $(/usr/sbin/sysctl -n vm.loadavg)
    shift
  fi

  [[ -n "$1" ]] && {
    local -i load_average=$(($1 / $ncpu)) # round down fraction
    local brightness=dark

    local hostname="${(%):-'%m'}"
    case "$brightness-$load_average" in
    *-0)     print -n "${hostname}"    ;;
    light-1) print -n "${hostname}"    ;;
    dark-1)  print -n "${hostname}"  ;;
    light-2) print -n "${hostname}"   ;;
    dark-2)  print -n "${hostname}" ;;
    *)       print -n "${hostname}"     ;;
    esac
  }
}

function prompt_sakuro_pwd() {
  local pwd_symbol="\uf115"
  local result
  local vcs_info_msg_0_ vcs_info_msg_1_
  LANG=C vcs_info

  local base_path="$(print -P '%~')"
  # remove repository_part($vcs_info_msg_1_)
  [[ -n "$vcs_info_msg_0_" && $vcs_info_msg_1_ != '.' ]] && base_path="${base_path%/$vcs_info_msg_1_}"

  result="${pwd_symbol} $base_path"
  [[ -n "$vcs_info_msg_1_" && "$vcs_info_msg_1_" != '.' ]] && result+="/$vcs_info_msg_1_"

  print -n "${result}"
}

function prompt_sakuro_vcs() {
  local vcs_symbol="\ue725"
  local vcs_info_msg_0_
  LANG=C vcs_info

  [[ -n "$vcs_info_msg_0_" ]] && print -n "${vcs_symbol} $vcs_info_msg_0_"
}

function prompt_sakuro_aws() {
  local aws_symbol="\uf270"
  [[ -n "$AWS_PROFILE" ]] && print -n "${aws_symbol} $AWS_PROFILE"
}

function prompt_sakuro_kube() {
  local kube_symbol="\u2388"
  if (( $+commands[kubectl] )); then
    local kube_context=$(kubectl config current-context 2> /dev/null)
    [[ -n "$kube_context" ]] && {
      print -n "${kube_symbol}$kube_context"
      local kube_namespace=$(kubectl config view --output json | jq -rM '.contexts[] | select(.name == "'$kube_context'").context.namespace')
      [[ -n "$kube_namespace" ]] && print -n ":$kube_namespace"
    }
  fi
}

function prompt_sakuro_ruby() {
  (( $+commands[mise] )) || return

  local ruby_symbol="\ue23e"
  local ruby_version=$(mise list --current --json ruby | jq -Mr '.[0].version')
  [[ $? == 0 ]] && print -n "${ruby_symbol} ${ruby_version}"
}

function prompt_sakuro_prompt() {
  local last_status=$1
  local symbol='$'
  local style

  if [[ $last_status = 0 ]]; then
    style='green'
  else
    style='red'
  fi

  print -n "$symbol"
}

prompt_sakuro_setup "$@"
