#!/bin/zsh

function prompt_sakuro_setup()
{
  zstyle ':vcs_info:*'     formats       '%F{blue}%b%f' '%S'
  zstyle ':vcs_info:*'     actionformats '%b%F{cyan}:%F{red}%a%f'
  zstyle ':vcs_info:svn:*' branchformat  '%F{blue}%b%F{cyan}:%fr%r'
  zstyle ':vcs_info:*' enable git svn hg bzr cvs

  add-zsh-hook precmd prompt_sakuro_precmd
}

function prompt_sakuro_precmd()
{
  autoload -Uz vcs_info

  psvar=()
  local -a first_line=()
  local -a main_line=()
  local -a final_line=()

  main_line+="$(prompt_sakuro_user)@$(prompt_sakuro_host)"
  main_line+="%F{yellow}%D{%H:%M:%S}%f"
  main_line+="$(prompt_sakuro_pwd)"
  main_line+="$(prompt_sakuro_vcs)"
  main_line+="$(prompt_sakuro_aws)"

  final_line+="$(prompt_sakuro_prompt)"

  psvar=(
    "${(j: :)first_line}"
    "${(j: :)main_line}"
    "${(j: :)final_line}"' '
  )
  PROMPT=${(F)psvar}
}

function prompt_sakuro_user() {
  print -nP "%n"
}

function prompt_sakuro_host() {
  print -nP "$(load-average-color)%m%f"
}

function prompt_sakuro_tmux_pane() {
  [[ -n "$TMUX_PANE" ]] && print -nP "%%%F{blue}$TMUX_PANE%f"
}

function prompt_sakuro_pwd() {
  local result
  local vcs_info_msg_0_ vcs_info_msg_1_
  LANG=C vcs_info

  local base_path="$(print -P '%~')"
  # remove repository_part($vcs_info_msg_1_)
  [[ -n "$vcs_info_msg_0_" && $vcs_info_msg_1_ != '.' ]] && base_path="${base_path%/$vcs_info_msg_1_}"

  typeset -la path_elements
  set -A path_elements ${(s:/:)base_path}

  if [[ $#path_elements -gt 2 ]]; then
    local first="$path_elements[1]"
    [[ "$first" = '~'* ]] || first="/$first"
    local last="$path_elements[-1]"
    base_path="$first/"${(j:/:)$(print -P '%2>.>'${^${(@)path_elements[2,-2]}})}/"$last"
  fi

  result+="%F{magenta}$base_path%f"
  [[ -n "$vcs_info_msg_1_" && "$vcs_info_msg_1_" != '.' ]] && result+="/%F{cyan}$vcs_info_msg_1_%f"

  print -n "${result}"
}

function prompt_sakuro_vcs() {
  local vcs_info_msg_0_
  LANG=C vcs_info

  [[ -n "$vcs_info_msg_0_" ]] && print -n "$vcs_info_msg_0_"
}

function prompt_sakuro_aws() {
  local aws_emoji="\uf270 "
  [[ -n "$AWS_PROFILE" ]] && print -nP "%F{yellow}${aws_emoji}$AWS_PROFILE%f"
}

function prompt_sakuro_prompt() {
  local last_status=$1
  print -nP '%(!.#.$)'
}

prompt_sakuro_setup "$@"
