#!/bin/zsh

function prompt_sakuro_setup() {
  zstyle ':vcs_info:*'     formats       '%{%F{blue}%}%b%{%f%}' '%S'
  zstyle ':vcs_info:*'     actionformats '%{%b%F{cyan}%}%{%F{red}%}%a%{%f%}'
  zstyle ':vcs_info:*' enable git

  source ~/.config/zsh/prompts/prompt_duration
  add-zsh-hook precmd prompt_sakuro_precmd
}

function prompt_sakuro_precmd() {
  local last_status=$?
  autoload -Uz vcs_info

  psvar=()

  [[ -n "$prompt_duration" ]] && print -P "%{%F{yellow}%}$prompt_duration%{%f%}"

  local -a info_line_elements=(
#    "$(prompt_sakuro_user)@$(prompt_sakuro_host)"
    "$(prompt_sakuro_pwd)"
    "$(prompt_sakuro_vcs)"
    "$(prompt_sakuro_ruby)"
    "$(prompt_sakuro_kube)"
    "$(prompt_sakuro_aws)"
  )
  local -a prompt_line=(
    "$(prompt_sakuro_prompt $last_status)"
  )

  psvar+="${(j: :)info_line_elements}"
  psvar+="${prompt_line} "

  PROMPT=${(F)psvar}
}

function prompt_sakuro_user() {
  print -nP "%n"
}


function prompt_sakuro_host() {
  local -i ncpu

  if [[ -f /proc/cpuinfo ]]; then
    set -- ${(M)"${(@f)$(</proc/cpuinfo)}"#processor*:*}
    ncpu=$#
  else
    ncpu=$(sysctl -n hw.ncpu)
  fi

  if [[ -r /proc/loadavg ]]; then
    set -- $(< /proc/loadavg)
  elif [[ -x /usr/sbin/sysctl ]]; then
    set -- $(/usr/sbin/sysctl -n vm.loadavg)
    shift
  fi

  [[ -n "$1" ]] && {
    local -i load_average=$(($1 / $ncpu)) # round down fraction
    local brightness=dark

    case "$brightness-$load_average" in
    *-0)     print -nP '%{%F{gray}%}%m%{%f%}'    ;;
    light-1) print -nP '%{%F{cyan}%}%m%{%f%}'    ;;
    dark-1)  print -nP '%{%F{yellow}%}%m%{%f%}'  ;;
    light-2) print -nP '%{%F{green}%}%m%{%f%}'   ;;
    dark-2)  print -nP '%{%F{magenta}%}%m%{%f%}' ;;
    *)       print -nP '%{%F{red}%}%m%{%f%}'     ;;
    esac
  }
}

function prompt_sakuro_pwd() {
  local pwd_symbol="\uf115"
  local result
  local vcs_info_msg_0_ vcs_info_msg_1_
  LANG=C vcs_info

  local base_path="$(print -P '%~')"
  # remove repository_part($vcs_info_msg_1_)
  [[ -n "$vcs_info_msg_0_" && $vcs_info_msg_1_ != '.' ]] && base_path="${base_path%/$vcs_info_msg_1_}"

  typeset -la path_elements
  set -A path_elements ${(s:/:)base_path}

  if [[ $#path_elements -gt 2 ]]; then
    local first="$path_elements[1]"
    [[ "$first" = '~'* ]] || first="/$first"
    local last="$path_elements[-1]"
    base_path="$first/"${(j:/:)$(print -P '%2>.>'${^${(@)path_elements[2,-2]}})}/"$last"
  fi

  result="%{%F{magenta}%}${pwd_symbol} $base_path%{%f%}"
  [[ -n "$vcs_info_msg_1_" && "$vcs_info_msg_1_" != '.' ]] && result+="/%{%F{cyan}%}$vcs_info_msg_1_%{%f%}"

  print -n "${result}"
}

function prompt_sakuro_vcs() {
  local vcs_symbol="\ue725"
  local vcs_info_msg_0_
  LANG=C vcs_info

  [[ -n "$vcs_info_msg_0_" ]] && print -n "%{%F{blue}%}${vcs_symbol}%{%f%} $vcs_info_msg_0_"
}

function prompt_sakuro_aws() {
  local aws_symbol="\uf270"
  [[ -n "$AWS_PROFILE" ]] && print -nP "%{%F{yellow}%}${aws_symbol} $AWS_PROFILE%{%f%}"
}

function prompt_sakuro_kube() {
  local kube_symbol="\u2388"
  if (( $+commands[kubectl] )); then
    local kube_context=$(kubectl config current-context 2> /dev/null)
    [[ -n "$kube_context" ]] && {
      print -nP "%{%F{cyan}%}${kube_symbol}$kube_context%{%f%}"
      local kube_namespace=$(kubectl config view --output json | jq -rM '.contexts[] | select(.name == "'$kube_context'").context.namespace')
      [[ -n "$kube_namespace" ]] && print -nP ":%{%F{cyan}%}$kube_namespace%{%f%}"
    }
  fi
}

function prompt_sakuro_ruby() {
  (( $+commands[mise] )) || return

  local ruby_symbol="\ue23e"
  local ruby_version=$(mise list --current --json ruby | jq -Mr '.[0].version')
  [[ $? == 0 ]] && print -nP "%{%F{red}%}${ruby_symbol} ${ruby_version}%{%f%}"
}

function prompt_sakuro_prompt() {
  local last_status=$1
  local symbol='$'
  local style

  if [[ $last_status = 0 ]]; then
    style='green'
  else
    style='red'
  fi

  print -nP "%{%F{$style}%}$symbol%{%f%}"
}

prompt_sakuro_setup "$@"
